
// Transform an automaton to a right linear grammar
//if str is 1 the function returns a string
//if str is 0 the fonction returns a grammar
export function automaton2RightLinearGrammar (A,str)
    let G := new linearGrammar()

    let alphabet := A.getAlphabet()
    let states := A.getStates()
    let finalStates := A.getFinalStates()
    let transi := A.getTransitions()

    //Set the non terminal symbol with the states of the automaton
    foreach s in states do
        G.addNonTerminalSymbol(s)
    done

    //Set the start symbol
    G.setStartSymbol(A.getInitialState())

    //Set the non terminal symbol with the alphabet of the automaton
    foreach c in alphabet do
        G.addTerminalSymbol(c)
    done

    //For each final states we add a rule s -> epsilon
    foreach s in finalStates do
         G.addRule(s,epsilon,"")
    done

    foreach t in transi do
        if t.symbol = epsilon then
            G.addRule(t.startState,"",t.endState)
        else
            G.addRule(t.startState,t.symbol,t.endState)
        end if
    done

    if str=0 or str=undefined then
        return(G)
    else if str=1 then
        return G.toString()
    end if

end function


run(
    function()

    let A := get_automaton(currentAutomaton)
        return (automaton2RightLinearGrammar(A,1))
    end function
)
