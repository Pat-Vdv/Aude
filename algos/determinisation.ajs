include epsElim;

function determinize(A) {
  console.dir(A);
  A = epsElim(A);
  DetA : Automaton;
  Alphabet : Set = A.getAlphabet();
  table : List = A.getTransitionsTable();
  
  function addDeterminizedState(newQ) {
    if(!DetA.hasState(newQ)) {
      DetA.addState(newQ);
      
      foreach(symbol in Alphabet) {
        QDest : Set;
        
        foreach(state in newQ) {
          QDest U= table[state][symbol];
        }
        addDeterminizedState(QDest);
        
        DetA.addTransition(newQ, symbol, QDest);
      }
    }
    
    foreach(state in newQ) {
      if(A.isFinalState(state)) {
        DetA.setFinalState(newQ);
        break;
      }
    }
  }
  
  QInit : Set;
  
  QInit.add(A.getInitialState());
  addDeterminizedState(QInit);
  
  DetA.setInitialState(QInit);
  
  return DetA;
}

run(determinize, get_automaton(currentAutomaton));