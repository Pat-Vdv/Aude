include epsElim;

function infiniteLanguage(A) {
  A     : Automaton = epsElim(A);
  table : Table = A.getTransitionsTable();
  
  function onCycle(consideredState, visited, curState) {
    if(visited contains curState) {
      return false;
    }
    
    visited.add(curState);
    
    for(var symbol in table[curState]) {
      foreach(state in table[curState][symbol]) {
        if(state == consideredState || onCycle(consideredState, visited.copy(), state)) {
          return true;
        }
      }	
    }
    return false;
  }
    
  function visit(state, visited, cycle) {
    if(visited contains state) {
      return false;
    }

    if(!cycle) {
      cycle = onCycle(state, emptySet(State), state);
    }
    
    if(cycle && A.isFinalState(state)) {
      return true;
    }
    
    visited.add(state);
    
    foreach(destStates in table[state]) {
      foreach(destState in destStates) {
        if(visit(destState, visited.copy(), cycle)) {
          return true;
        }
      }
    }
    return false;
  }
    
  return visit(A.getInitialState(), emptySet(State), false);
}

run(infiniteLanguage, get_automaton(currentAutomaton));