function complete(A, alphabet) {
   compA : Automaton;
   let trans = A.getTransitionFunction();

   if(alphabet) {
      alphabet U= A.getAlphabet();
   }
   else {
      alphabet = A.getAlphabet();
   }

   compA.setInitialState(A.getInitialState());

   let well = 'W';
   while(A.hasState(well)) {
      well += 'W';
   }

   foreach(state in A.getStates()) {
      foreach(symbol in alphabet) {
         if(trans(state, symbol).isEmpty()) {
            compA.addState(well);
            compA.addTransition(state, symbol, well);
         }
      }
   }
   if(compA.hasState(well)) {
      foreach(a in compA.getAlphabet()) {
         compA.addTransition(well, a, well);
      }
   }

   foreach(state in A.getFinalStates()) {
      compA.setFinalState(state);
   }

   foreach(t in A.getTransitions()) {
      compA.addTransition(t);
   }

   return compA;
}

run(complete, get_automaton(currentAutomaton));