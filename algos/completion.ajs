function complete(A, alphabet) {
   compA : Automaton;
   let trans = A.getTransitionFunction();

   if(alphabet) {
      alphabet U= A.getAlphabet();
   }
   else {
      alphabet = A.getAlphabet();
   }

   compA.setInitialState(A.getInitialState());

   let sink = 'S';
   while(A.hasState(sink)) {
      sink += 'S';
   }

   foreach(state in A.getStates()) {
      foreach(symbol in alphabet) {
         if(trans(state, symbol).isEmpty()) {
            compA.addState(sink);
            compA.addTransition(state, symbol, sink);
         }
      }
   }
   if(compA.hasState(sink)) {
      foreach(a in alphabet) {
         compA.addTransition(sink, a, sink);
      }
   }

   foreach(state in A.getFinalStates()) {
      compA.setFinalState(state);
   }

   foreach(t in A.getTransitions()) {
      compA.addTransition(t);
   }

   return compA;
}

run(complete, get_automaton(currentAutomaton));
